import org.apache.tools.ant.filters.ReplaceTokens
plugins {
  id "com.github.hierynomus.license" version "0.11.0"
  id 'nebula.nebula-release' version '2.2.5'
}
apply plugin: "scala"

group 'com.xebialabs.xlplatform.plugin'

def xlReleaseHome = project.hasProperty('xlReleaseHome') ? project.xlReleaseHome : System.env.XLRELEASE_HOME
if (!xlReleaseHome) {
  throw new RuntimeException("Please specify location of unpacked XL Release server installation in your " +
      "gradle.properties or with '-PxlReleaseHome=/path/to/xl/release/server' command line option")
}

dependencies {
  compile "org.scala-lang:scala-library:2.10.4"
  compile fileTree(dir: "$xlReleaseHome/lib", include: ['*.jar'])

  testCompile "junit:junit:4.11"
  testCompile "org.scalatest:scalatest_2.10:2.2.0"
}

release {
  defaultVersionStrategy = nebula.plugin.release.NetflixOssStrategies.SNAPSHOT
}
if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

processResources.configure {
  filter ReplaceTokens, tokens: [
      'project.version': version.toString(),
      'project.name': rootProject.name
  ]
}

license {
  header rootProject.file('src/main/license/xebialabs_community.license')
  strictCheck true
  excludes(["**/*.json", "**/*.conf"])
}

repositories {
  mavenLocal()
  mavenCentral()
}